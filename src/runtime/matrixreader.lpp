/***************************************************************************
 *  Copyright (C) 2008-2009 Massachusetts Institute of Technology          *
 *                                                                         *
 *  This source code is part of the PetaBricks project and currently only  *
 *  available internally within MIT.  This code may not be distributed     *
 *  outside of MIT. At some point in the future we plan to release this    *
 *  code (most likely GPL) to the public.  For more information, contact:  *
 *  Jason Ansel <jansel@csail.mit.edu>                                     *
 *                                                                         *
 *  A full list of authors may be found in the file AUTHORS.               *
 ***************************************************************************/
%option caseless
%option nostdinit
%option noyywrap
%option nounput
%option prefix="matrixreader"
%option outfile="lex.yy.c"

%{

#include "matrixio.h"

#include "common/jassert.h"
#include "common/jconvert.h"

#ifdef HAVE_CONFIG_H
#  include "config.h"
#endif

#include <stdio.h>

using namespace petabricks;

#define YY_DECL int matrixreaderlex(MatrixReaderScratch& o)

YY_DECL;

%}

%x DATA
%x SIZE

%%

<INITIAL>{
  [ \r\t]+              //nothing
  [\n]                  //nothing
  "size"                {
    o.dimensions=0;
    BEGIN(SIZE);
  }
}

<SIZE>{
  [ \r\tx]+              //nothing
  [0-9]+                {
    JASSERT(o.dimensions+1 < MAX_DIMENSIONS)(o.dimensions)
      .Text("Input matrix has too many dimensions, increase MAX_DIMENSIONS in config.h");
    o.sizes[o.dimensions++] = jalib::StringToX<int>(yytext);
  }
  [\n]                  {
    int n=1;
    for(int i=0; i<o.dimensions; ++i) n*=o.sizes[i];
    o.storage=new petabricks::MatrixStorage(n);
    o.buf = o.storage->data();
    o.remaining = n;
    BEGIN(DATA);
  }
}

<DATA>{
  [ \r\t]+              //nothing
  [\n]                  //nothing
  [-]?[0-9]+[.]?[0-9]*[e]?[+-]?[0-9]* {
    JASSERT(o.remaining>0).Text("Buffer underflow while reading input matrix");
    *o.buf++ = jalib::StringToX<MATRIX_ELEMENT_T>(yytext);
    if(--o.remaining == 0){
      BEGIN(INITIAL);
      return 0;
    }
  }
}

<*>{
  . JASSERT(false)(yytext).Text("Unhandled input");
}

%%



