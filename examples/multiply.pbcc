
#include "add.pbcc"

transform MatrixMultiply
from A[c,h], B[w,c] 
to AB[w,h]
{
  //recursively chop the matrix in half
  recursive(c) 
  to(AB ab)
  from( A.region(0,   0, c/2, h  ) a1,
        A.region(c/2, 0, c,   h  ) a2,
        B.region(0,   0, w,   c/2) b1,
        B.region(0, c/2, w,   c  ) b2)
  {
    MatrixRegion2D tmp = MatrixRegion2D::allocate(w,h);
    SPAWN(MatrixMultiply(ab,  a1, b1));
    SPAWN(MatrixMultiply(tmp, a2, b2));
    SYNC();
    SPAWN(MatrixAdd(ab, ab, tmp));
  }
  
  //compute a cell the straightforward way
  AB.cell(x,y) from(A.row(y) a, B.column(x) b){
    ElementT sum=0;
    for(IndexT i=0; i<a.width(); ++i)
      sum+=a.cell(i)*b.cell(i);
    return sum;
  }
}

