//Multigrid V-cycle 2D
//assume dimension n = 2^k + 1

#include "../simple/add.pbcc"
#include "Interpolate2Dnew.pbcc"
#include "Restrict2Dnew.pbcc"
#include "Poisson2DResidual.pbcc"
#include "Poisson2DSOR.pbcc"
#include "Poisson2DBS.pbcc"
#include "Poisson2DAccuracyMetric.pbcc"

// Uncomment the following define to enable debug execution
// #define DEBUG

transform recursiveMGV
from X[n,n], B[n,n], Bin, Iters
through Z[(n+1)/2,(n+1)/2]
to Y[n,n]
{
    to (Z.cell(i,j) z) from () {
        z = 0;
    }

    to (Y y) from (X x, B b, Z z, Bin bin, Iters iters)
    {
        int level = (int) log2(n - 1);

        if (level <= 1) {

            PetabricksRuntime::abort();

        } else {

            int i;
            MatrixRegion2D t1 = MatrixRegion2D::allocate(n, n);
            MatrixRegion2D t2 = MatrixRegion2D::allocate(n, n);
            MatrixRegion2D r1 = MatrixRegion2D::allocate((n+1)/2, (n+1)/2);
            MatrixRegion2D r2 = MatrixRegion2D::allocate((n+1)/2, (n+1)/2);

            // copy input to output
            Copy2D(y, x);

            // iterate the recursive call for iters iterations
            for (i = 0; i < (int) iters; i++) {

                // compute residual and solve on coarser grid with zero
                // initial guess, then map the coarse solution back to the
                // fine grid by interpolation

                Poisson2DSOR(t1, y, b, -2, 1);        // t1 gets relaxed guess
                Poisson2DResidual(t2, t1, b);         // t2 gets new residual
                Restrict2Dnew(r1, t2);                // r1 gets restricted residual
                Poisson2DSolve<(int) bin>(r2, z, r1); // r2 gets coarse update
                Interpolate2Dnew(t2, r2);             // t2 gets interpolated update
                MatrixAdd(t2, t2, t1);                // t2 gets updated guess
                Poisson2DSOR(y, t2, b, -2, 1);        //  y gets relaxed guess
            }
        }
    }
}

transform Poisson2DSolve
from X[n,n], B[n,n]
to  Y[n,n]
accuracy_bins 10, 1000, 100000, 10000000, 1000000000
accuracy_variable numIterations
accuracy_metric Poisson2DAccuracyMetric
{
    //base case: directly solve with BS
    to(Y y) from(X x, B b) {

        int level = (int) log2(n-1);

        // for level greater than 8, just abort since
        // this will likely be slow and not the best choice
        if (level > 8) {
            PetabricksRuntime::abort();
        }

#ifdef DEBUG
        printf("BS case called for n = %d, acc %d\n", n, _acc_bin);
#endif

        Poisson2DBS(y, x, b);

    }

    //base case: solve iteratively with SOR
    to(Y y) from(X x, B b) {

        int level = (int) log2(n-1);

        // for level greater than 4, just abort since
        // this will likely be slow and not the best choice
        if (level > 4) {
            PetabricksRuntime::abort();
        }

#ifdef DEBUG
        printf("SOR case called for n = %d, acc %d (%d iterations)\n", n, _acc_bin, numIterations);
#endif

        Poisson2DSOR(y, x, b, -1, numIterations);
    }

    //solve recursively on coarser grids

    //recursive case with accuracy bin 0
    recursive(n)
    to (Y y) from(X x, B b) {
#ifdef DEBUG
        printf("recursive 0 case called for n = %d, acc %d (%d iterations)\n", (int) n, _acc_bin, numIterations);
#endif
        recursiveMGV(y, x, b, 0, numIterations);
    }

    //recursive case with accuracy bin 1
    recursive(n)
    to (Y y) from(X x, B b) {
#ifdef DEBUG
        printf("recursive 1 case called for n = %d, acc %d (%d iterations)\n", (int) n, _acc_bin, numIterations);
#endif
        recursiveMGV(y, x, b, 1, numIterations);
    }

    //recursive case with accuracy bin 2
    recursive(n)
    to (Y y) from(X x, B b) {
#ifdef DEBUG
        printf("recursive 2 case called for n = %d, acc %d (%d iterations)\n", (int) n, _acc_bin, numIterations);
#endif
        recursiveMGV(y, x, b, 2, numIterations);
    }

    //recursive case with accuracy bin 3
    recursive(n)
    to (Y y) from(X x, B b) {
#ifdef DEBUG
        printf("recursive 3 case called for n = %d, acc %d (%d iterations)\n", (int) n, _acc_bin, numIterations);
#endif
        recursiveMGV(y, x, b, 3, numIterations);
    }

    //recursive case with accuracy bin 4
    recursive(n)
    to (Y y) from(X x, B b) {
#ifdef DEBUG
        printf("recursive 4 case called for n = %d, acc %d (%d iterations)\n", (int) n, _acc_bin, numIterations);
#endif
        recursiveMGV(y, x, b, 4, numIterations);
    }
}
