#ifndef KERNELESTIMATE_PBCC
#define KERNELESTIMATE_PBCC

#include "../linearsolve/LinearSolvePosDef.pbcc"
#include "../simple/dotprod.pbcc"
#include "utils.pbcc"

%{

template < typename T >
inline ElementT kernel(T x0, T x1, T h)
{
  x0.useOnCpu();
  x1.useOnCpu();
  h.useOnCpu();

  int i, dim = x0.count();
  ElementT temp, sum = 0;
  for (i = 0; i < dim; i++) {
    temp = (x0.cell(i) - x1.cell(i)) / h.cell(i);
    sum += temp * temp;
  }
  return exp(-sum);
}

%}

// evaluates kernel function between X0 and each of columns of X
// using bandwidth H
transform KernelEvaluate
from X0[d], X[d,n], H[d]
to OUT[n]
{
  OUT.cell(i) from (X0 x0, X.row(i) x, H h)
  {
    return kernel(x0, x, h);
  }
}

// n training samples, m test samples, d dimensions
// K is the Gram matrix of kernel inner products between training vectors
// A is the vector of regression coefficients
// H is the tunbale bandwidth of the kernel
transform KernelEstimate
from TRAINX[d,n], TRAINY[1,n], TESTX[d,m], H[d], LAMBDA
through A[n,1], K[n,n]
to ESTIMATEY[m]
{
  K.cell(i,j) from (TRAINX.row(i) xi,
                    TRAINX.row(j) xj,
                    H h, LAMBDA lambda)
  {
      ElementT temp = kernel(xi, xj, h);
      return (i == j) ? temp + lambda : temp;
  }

  to (A a) from (K k, TRAINY trainY, LAMBDA lambda)
  {
    LinearSolvePosDef(a, k, trainY);
    Scale2D(a, lambda);
  }

  ESTIMATEY.cell(i) from (TRAINX trainX, TESTX.row(i) testX, A a, H h)
  {
    ElementT result;
    MatrixRegion1D ke = MatrixRegion1D::allocate(n);
    KernelEvaluate(ke, testX, trainX, h);
    DotProduct(result, ke, a.row(0));
    return result;
  }
}

#endif // KERNELESTIMATE_PBCC
