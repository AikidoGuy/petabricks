#ifndef MGV2D_PBCC
#define MGV2D_PBCC

//Multigrid V-cycle 2D
//assume dimension n = 2^k + 1
#include "../simple/add.pbcc"
#include "../simple/copy.pbcc"
#include "Interpolate2Dnew.pbcc"
#include "Restrict2Dnew.pbcc"
#include "Residual2D.pbcc"
#include "SOR2D.pbcc"

// performance note: Y can point to the same matrix region as X
transform mgv2D_inner 
from X[n,n], B[n,n]
to  Y[n,n]
through zeros[(n+1)/2,(n+1)/2]
{
    //solve recursively on coarser grids, with zero initial guess
    //then map the coarse solution back to the fine grid by Interpolation
    zeros.cell(i,j) from() {
        return 0;
    }

    //recursive case
    recursive(n / 2)
    to (Y y) from(X x, B b, zeros z) {
        JASSERT(floor(log2(n-1)) == log2(n-1))(n).Text("input must be 2^k+1");
        if (n == 3) {
          printf("base case called for n = 3\n");
          Copy2D(y, x);
          y.cell(1, 1) = (x.cell(1, 0) + x.cell(0, 1) + x.cell(1, 2) + x.cell(2, 1) + b.cell(1, 1)) / 4;
        } else {
          printf("recursive case called for n = %d\n", n);
          MatrixRegion2D t1 = MatrixRegion2D::allocate(n, n);
          MatrixRegion2D t2 = MatrixRegion2D::allocate(n, n);
          MatrixRegion2D r1 = MatrixRegion2D::allocate((n+1)/2, (n+1)/2);
          MatrixRegion2D r2 = MatrixRegion2D::allocate((n+1)/2, (n+1)/2);
          SOR2D(t1, x, b, -2, 1);   // t1 gets relaxed guess
          Residual2D(t2, t1, b);    // t2 gets new residual
          Restrict2Dnew(r1, t2);    //  r gets restricted residual
          mgv2D_inner(r2, z, r1);   //  r gets coarse update
          Interpolate2Dnew(t2, r2); // t2 gets interpolated update
          MatrixAdd(t2, t2, t1);    // t2 gets updated guess
          SOR2D(y, t2, b, -2, 1);   //  y gets relaxed guess
        }
    }
}

// performance note: Y can point to the same matrix region as X
transform mgv2D
from X[n,n], B[n,n], numIterations
to Y[n,n]
{
  recursive
  to (Y y) from (X x, B b, numIterations num)
  {
    if (num > 0) {
      SPAWN(mgv2D_inner, y, x, b);
    } else {
      SPAWN(Copy2D, y, x);
    }
    SYNC();
    if (num > 1)
      SPAWN(mgv2D, y, y, b, num - 1);
  }
}

#endif // MGV2D_PBCC

