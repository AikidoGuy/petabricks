#ifndef QR_PBCC
#define QR_PBCC

#include "../simple/addInPlace.pbcc"
#include "../simple/copy.pbcc"
#include "../simple/dotprod.pbcc"
#include "../simple/norm.pbcc"
#include "../simple/scale.pbcc"

transform Init1D
from VALUE
to A[n]
{
    to (A.cell(i) a) from (VALUE val)
    {
        a = val;
    }
}

transform Init2D
from VALUE
to A[n,m]
{
    to (A.cell(j,i) a) from (VALUE val)
    {
        a = val;
    }
}

transform QRInPlace
to Q[n,m], R[n,n]
{
    // TODO: Householder

    // Gram-Schmidt
    to (Q q, R r) from ()
    {
        IndexT i, j;
        Init2D(r, 0);
        for (i = 0; i < n; ++i) {
            Norm1D(r.cell(i, i), q.col(i), 2.0);
            Scale1D(q.col(i), 1.0 / r.cell(i, i));
            for (j = i + 1; j < n; ++j) {
                DotProduct(r.cell(j, i), q.col(i), q.col(j));
                AddInPlace1D(q.col(j), -r.cell(j, i), q.col(i), 1.0);
            }
        }
    }
}

transform QR
from A[n,m]
to Q[n,m], R[n,n]
{
    to (Q q, R r)
    from (A a)
    {
        Copy2D(q, a);
        QRInPlace(q, r);
    }
}

#endif // QR_PBCC
